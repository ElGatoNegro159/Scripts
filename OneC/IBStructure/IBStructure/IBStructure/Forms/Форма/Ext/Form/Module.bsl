
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтруктураБД = ПолучитьСтруктуруХраненияБазыДанных();
	ЗначениеВРеквизитФормы(СтруктураБД, "ТаблицаСтруктураБД");
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтруктуруХранения(Команда)
	
	ТабДок = ВывестиСтруктуруХраненияНаСервере();
	ТабДок.Показать();
	
КонецПроцедуры

&НаСервере
Функция ВывестиСтруктуруХраненияНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	КвалификаторСтроки = Новый КвалификаторыСтроки(250);
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка",,,,КвалификаторСтроки);
	
	ОписаниеТиповУИД = Новый ОписаниеТипов("УникальныйИдентификатор");
	
	ТаблицыБД = Новый ТаблицаЗначений;
	ТаблицыБД.Колонки.Добавить("УИД",ОписаниеТиповУИД);
	ТаблицыБД.Колонки.Добавить("ИмяТаблицы",ОписаниеТиповСтрока);
	ТаблицыБД.Колонки.Добавить("ИмяТаблицыХранения",ОписаниеТиповСтрока);
	ТаблицыБД.Колонки.Добавить("Метаданные",ОписаниеТиповСтрока);
	ТаблицыБД.Колонки.Добавить("Назначение",ОписаниеТиповСтрока);
	
	ТаблицыБДПоля = Новый ТаблицаЗначений;
	ТаблицыБДПоля.Колонки.Добавить("УИД",ОписаниеТиповУИД);
	ТаблицыБДПоля.Колонки.Добавить("УИДПоля",ОписаниеТиповУИД);
	ТаблицыБДПоля.Колонки.Добавить("ИмяПоля",ОписаниеТиповСтрока);
	ТаблицыБДПоля.Колонки.Добавить("ИмяПоляХранения",ОписаниеТиповСтрока);
	ТаблицыБДПоля.Колонки.Добавить("Метаданные",ОписаниеТиповСтрока);
	
	ТаблицыБДИндексы = Новый ТаблицаЗначений;
	ТаблицыБДИндексы.Колонки.Добавить("УИД",ОписаниеТиповУИД);
	ТаблицыБДИндексы.Колонки.Добавить("УИДИндекса",ОписаниеТиповУИД);
	ТаблицыБДИндексы.Колонки.Добавить("ИмяИндексаХранения",ОписаниеТиповСтрока);
	ТаблицыБДИндексы.Колонки.Добавить("Метаданные",ОписаниеТиповСтрока);
	ТаблицыБДИндексы.Колонки.Добавить("Назначение",ОписаниеТиповСтрока);
	
	ТаблицыБДПоляИндексов = Новый ТаблицаЗначений;
	ТаблицыБДПоляИндексов.Колонки.Добавить("УИДИндекса",ОписаниеТиповУИД);
	ТаблицыБДПоляИндексов.Колонки.Добавить("УИДПоляИндекса",ОписаниеТиповУИД);
	ТаблицыБДПоляИндексов.Колонки.Добавить("ИмяПоля",ОписаниеТиповСтрока);
	ТаблицыБДПоляИндексов.Колонки.Добавить("ИмяПоляХранения",ОписаниеТиповСтрока);
	ТаблицыБДПоляИндексов.Колонки.Добавить("Метаданные",ОписаниеТиповСтрока);
	
	Для Каждого Таблица Из ТаблицаСтруктураБД Цикл
		
		ЗаписьТаблица = ТаблицыБД.Добавить();
		
		ЗаписьТаблица.УИД = Новый УникальныйИдентификатор;
		ЗаписьТаблица.ИмяТаблицы = Таблица.ИмяТаблицы;
		ЗаписьТаблица.ИмяТаблицыХранения = Таблица.ИмяТаблицыХранения;
		ЗаписьТаблица.Метаданные = Таблица.Метаданные;
		ЗаписьТаблица.Назначение = Таблица.Назначение;
		
		Для Каждого ПолеТаблицы Из Таблица.Поля Цикл
			
			ЗаписьПоле = ТаблицыБДПоля.Добавить();
			
			ЗаписьПоле.УИД = ЗаписьТаблица.УИД;
			ЗаписьПоле.УИДПоля = Новый УникальныйИдентификатор;
			ЗаписьПоле.ИмяПоля = ПолеТаблицы.ИмяПоля;
			ЗаписьПоле.ИмяПоляХранения = ПолеТаблицы.ИмяПоляХранения;
			ЗаписьПоле.Метаданные = ПолеТаблицы.Метаданные;
			
		КонецЦикла;
		
		Для Каждого ИндексТаблицы Из Таблица.Индексы Цикл
			
			ЗаписьИндекс = ТаблицыБДИндексы.Добавить();
			
			ЗаписьИндекс.УИД = ЗаписьТаблица.УИД;
			ЗаписьИндекс.УИДИндекса = Новый УникальныйИдентификатор;
			ЗаписьИндекс.ИмяИндексаХранения = ИндексТаблицы.ИмяИндексаХранения;
			ЗаписьИндекс.Метаданные = ИндексТаблицы.Метаданные;
			ЗаписьИндекс.Назначение = ИндексТаблицы.Назначение;
			
			Для Каждого ПолеИндекса Из ИндексТаблицы.Поля Цикл
				
				ЗаписьПолеИндекса = ТаблицыБДПоляИндексов.Добавить();
				
				ЗаписьПолеИндекса.УИДИндекса = ЗаписьИндекс.УИД;
				ЗаписьПолеИндекса.УИДПоляИндекса = Новый УникальныйИдентификатор;
				ЗаписьПолеИндекса.ИмяПоля = ПолеИндекса.ИмяПоля;
				ЗаписьПолеИндекса.ИмяПоляХранения = ПолеИндекса.ИмяПоляХранения;
				ЗаписьПолеИндекса.Метаданные = ПолеИндекса.Метаданные;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ВнешниеНаборыДанных = Новый Структура;
    ВнешниеНаборыДанных.Вставить("ТаблицыБД", ТаблицыБД);
    ВнешниеНаборыДанных.Вставить("ТаблицыБДПоля", ТаблицыБДПоля);
    ВнешниеНаборыДанных.Вставить("ТаблицыБДИндексы", ТаблицыБДИндексы);
    ВнешниеНаборыДанных.Вставить("ТаблицыБДПоляИндексов", ТаблицыБДПоляИндексов);
    
    Схема = ОбработкаОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Схема.НастройкиПоУмолчанию);
	
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);

    Возврат ТабличныйДокумент;
	
КонецФункции

&НаКлиенте
Процедура СформироватьСкриптОчисткиДанных(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТаблицаСтруктураБД", ТаблицаСтруктураБД);
	
	ОткрытьФорму(
		"ВнешняяОбработка.СтруктураТаблицИБ.Форма.ФормаОчисткиДанных",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);

КонецПроцедуры
